apply plugin: 'war'
apply plugin: 'jetty'

description = 'Mutabra UI Module'

dependencies {
    compile project(':mutabra-domain')

    compile "ch.qos.logback:logback-classic:${versions.logback}"
    compile "org.apache.shiro:shiro-core:${versions.shiro}"
    compile "org.apache.shiro:shiro-web:${versions.shiro}"
    compile "org.apache.tapestry:tapestry-core:${versions.tapestry}"
    compile "org.scribe:scribe:${versions.scribe}"

    provided "javax.servlet:servlet-api:${versions.servletApi}"
    provided "javax.mail:mail:${versions.mailApi}"
}

task generateConfig(type: Copy) {
    from 'src/main/config'
    into "${buildDir}/config"
    include '*.xml'
    expand(project: project)

    if (isSnapshot) {
        from 'src/main/config/development'
    } else {
        from 'src/main/config/production'
    }
}

war {
    dependsOn generateConfig

    webXml file("${generateConfig.destinationDir}/web.xml")
    webInf {
        from("${generateConfig.destinationDir}/jetty-env.xml")
    }
}

jettyRun {
    dependsOn generateConfig

    scanIntervalSeconds 2
    contextPath '/'
    jettyEnvXml file("${generateConfig.destinationDir}/jetty-env.xml")
}
