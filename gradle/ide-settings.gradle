allprojects {
    apply plugin: 'idea'
}

subprojects {
    idea {
        module {
            inheritOutputDirs false
            outputDir sourceSets.main.output.classesDir
            testOutputDir sourceSets.test.output.classesDir

            scopes.PROVIDED.plus += configurations.provided
        }
    }
}

idea {
    project {
        ipr {
            withXml { provider ->
                //noinspection GroovyAssignabilityCheck
                def replaceComponent = { String name, Closure content ->
                    def node = provider.node.component.find { it.@name == name } ?: provider.node.appendNode('component')
                    node.replaceNode {
                        component(name: name, content)
                    }
                };

                replaceComponent('VcsDirectoryMappings') {
                    mapping(vcs: 'Git', directory: '$PROJECT_DIR$')
                }

                replaceComponent('CodeStyleSettingsManager') {
                    option(name: 'PER_PROJECT_SETTINGS') {
                        value new XmlParser().parse("${rootProject.projectDir}/gradle/config/code-style.xml").children()
                    }
                    option(name: 'USE_PER_PROJECT_SETTINGS', value: true)
                }
            }
        }
    }
}
