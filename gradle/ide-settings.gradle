allprojects {
    apply plugin: 'idea'
}

subprojects {
    idea {
        module {
            inheritOutputDirs false
            outputDir sourceSets.main.output.classesDir
            testOutputDir sourceSets.test.output.classesDir

            scopes.PROVIDED.plus += configurations.provided
        }
    }
}

idea {
    project {
        ipr {
            withXml { provider ->
                def mapping = provider.node.component.find { it.@name == 'VcsDirectoryMappings' }.mapping
                mapping.@vcs = 'Git'
                mapping.@directory = '$PROJECT_DIR$'

                def codeStyle = provider.node.component.find { it.@name == 'CodeStyleSettingsManager' } ?: provider.node.appendNode('component')
                codeStyle.replaceNode { node ->
                    component(name: 'CodeStyleSettingsManager') {
                        option(name: 'PER_PROJECT_SETTINGS') {
                            value new XmlParser().parse("${rootProject.projectDir}/gradle/config/code-style.xml").children()
                            
                        }
                        option(name: 'USE_PER_PROJECT_SETTINGS', value: true)
                    }
                }
            }
        }
    }
}
