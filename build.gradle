group = 'com.mutabra'
version = '1.0-pre-alpha-13-SNAPSHOT'
description = 'Mutabra CCG'

ext {
    isSnapshot = version.endsWith('-SNAPSHOT')

    versions = [
            java: '1.6',
            groovy: '2.0.6',
            gradle: '1.8',

            // common
            slf4j: '1.6.2',
            logback: '1.0.0',
            spock: '0.7-groovy-2.0',

            // domain
            morphia: '0.99',

            // ui
            shiro: '1.2.0',
            tapestry: '5.3.7',
            scribe: '1.3.2',
            // provided
            servletApi: '2.5',
            mailApi: '1.4.4'
    ]

}

allprojects {
    apply plugin: 'idea'

    repositories {
        mavenCentral()
        mavenLocal()

        maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
        maven { url "http://morphia.googlecode.com/svn/mavenrepo/" }
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'groovy'
    apply plugin: 'maven-publish'

    group = rootProject.group
    version = rootProject.version

    sourceCompatibility = versions.java
    targetCompatibility = versions.java

    configurations {
        provided
    }

    sourceSets {
        main {
            compileClasspath += configurations.provided
        }
        test {
            compileClasspath += configurations.provided
            runtimeClasspath += configurations.provided
        }
    }

    dependencies {
        testCompile "org.codehaus.groovy:groovy-all:${versions.groovy}"
        testCompile "org.spockframework:spock-core:${versions.spock}"

        testRuntime "org.slf4j:slf4j-simple:${versions.slf4j}"
    }

    test {
        useJUnit()

        maxHeapSize '512m'
    }

    jar {
        from(rootProject.projectDir) {
            include '*.md'
            into 'META-INF'
        }
    }

    idea.module {
        scopes.PROVIDED.plus += configurations.provided
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = versions.gradle
}
